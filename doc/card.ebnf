/** 
 * EBNF of CardProp
 * using W3C EBNF notation (https://www.w3.org/TR/xml/#sec-notation)
 * Use https://www.bottlecaps.de/rr/ui to render svg.
 */
CardProp ::= '{' version ',' id ',' data ',' avatars '}'

version ::= digits '.' digits
id ::= 'id: "' uuid4 '"'
data ::= 'data: ' char* '"'
avatars ::= 'avatars: {' (cardavatar ',')+ '}'

cardavatar ::= location ':' transformablefeature

transformablefeature ::= '{' geometry ',' style ',' condition ',' date '}'

geometry ::= '{' x ',' y ',' z ',' width ',' height '}'
x ::= 'x: ' digits
y ::= 'y: ' digits
z ::= 'z: ' digits
width ::= 'width: ' digits
height ::= 'height: ' digits

style ::= '{' uiColor ',' backgroundColor ',' opacity ',' zoom '}'
uiColor ::= 'uiColo: #' hex6
backgroundColor ::= 'backgroundColor: #' hex6
opacity ::= 'opacity: ' '1.0' | '0.' digits
zoom ::= 'zoom: ' digits '.' digits

condition ::= '{' locked '}'
locked ::= 'locked: ' boolean

date ::= '{' createdDate ',' modifiedDate '}'
createdDate ::= 'createdDate: ' year '-' month '-' day ' ' hour ':' minute ':' second
modifiedDate ::= 'modifiedDate: ' year '-' month '-' day ' ' hour ':' minute ':' second

/* The date format is only apporoximate here. */
year ::= [0-9] [0-9] [0-9] [0-9]
month ::= [0-1] [0-9]
day ::= [0-3] [0-9]
hour ::= [0-2] [0-9]
minute ::= [0-6] [0-9]
second ::= [0-6] [0-9]

boolean ::= 'true' | 'false'
digits ::= [0-9] | ([1-9]+ [0-9])
hex6 ::= hex hex hex hex hex hex
char ::= #x9 | #xA | #xD | [#x20-#xD7FF] | [#xE000-#xFFFD] | [#x10000-#x10FFFF] /* https://www.w3.org/TR/xml/#charsets */

/* import location from reactivedt.ebnf */

location ::= scheme '//' owner '/' ontology '/'
scheme ::= 'reactivedt:'
owner ::= userid | 'local'
userid ::= uuid4
ontology ::= 'card' | avatar
avatar ::= 'avatar' '/' workspaceid ( '/' cardid )*
workspaceid ::= [0-9] | [1-9]+ [0-9]
cardid ::= uuid4
uuid4 ::= hex8 '-' hex4 '-' '4' hex3 '-' hex4 '-'hex12
hex12 ::= hex4 hex4 hex4
hex8 ::= hex4 hex4
hex4 ::= hex hex hex hex
hex3 ::= hex hex hex
hex ::= [0-9a-f]
